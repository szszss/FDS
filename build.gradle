buildscript {
    repositories {
        jcenter()
		maven{url 'http://maven.oschina.net/content/groups/public/'}
    }
	
    dependencies {
		classpath 'ca.coglinc:javacc-gradle-plugin:2.3.1'
		//classpath 'com.android.tools.build:gradle:1.5.0'
		classpath 'org.akhikhl.gretty:gretty:1.2.4'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
	ext {
        appName = 'FDS'
    }

    repositories {
		jcenter()
		maven{url 'http://maven.oschina.net/content/groups/public/'}
    }
}

project(":core") {
    apply plugin: "java"
	apply plugin: "ca.coglinc.javacc"
	
	dependencies {
		testCompile "junit:junit:4.8.2"
	}
	
	sourceSets {
		main {
			java {
				srcDir "src/main/javacc"
			}
		}
	}
	
	ext {
		jjtPackage = 'src/main/javacc/net/hakugyokurou/fds/parser'
	}
	
	compileJjtree {
		inputDirectory = file(jjtPackage)
		outputDirectory = file(jjtPackage)
	}

	compileJavacc {
		inputDirectory = file(jjtPackage)
		outputDirectory = file(jjtPackage)
	}

	compileJavacc.doLast {
		delete file(jjtPackage + '/tmp')
	}

	eclipseClasspath.dependsOn("compileJjtree", "compileJavacc")
}

project(":cmd") {
    apply plugin: "java"
	
	dependencies {
		compile project(":core")
		compile "commons-cli:commons-cli:1.3.1"
		testCompile "junit:junit:4.8.2"
	}
	
	jar.manifest.attributes "Main-Class": "net.hakugyokurou.fds.cmd.FDSCmd"
	
	task copyWrappers(type: Copy) {
		from 'src/wrapper'
		into 'build/libs'
	}
	
	build.dependsOn copyWrappers
}

project(":desktop") {
    apply plugin: "java"
	
	dependencies {
		compile project(":core")
		testCompile "junit:junit:4.8.2"
	}
	
	jar.manifest.attributes "Main-Class": "net.hakugyokurou.fds.desktop.FDSApplication"
}

project(":web") {
    apply plugin: "java"
	apply plugin: "eclipse-wtp"
	apply plugin: "war"
	//apply plugin: "org.akhikhl.gretty"
	
	dependencies {
		compile project(":core")
		compile "com.jfinal:jfinal:2.2"
	}
	
	sourceSets {
		main {
			java {
				srcDir "src/main/webapp"
			}
		}
	}
	
	war.archiveName = "root.war"
}

/*project(":android") {
    apply plugin: "android"
	
	dependencies {
		compile files("libs/android-support-v4.jar")
		compile project(":core")
	}
}*/

tasks.eclipse.doLast {
    delete ".project"
}